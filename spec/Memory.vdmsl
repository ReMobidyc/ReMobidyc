module Memory
imports from AST all
exports all
definitions
types
     Address = nat1;

values
     ADDRESS_ERROR = <ADDRESS_ERROR>;
     TIME_ERROR = <TIME_ERROR>;

state Memory of
    vals : map Address to real
    next : map Address to real
    delta : map Address to real
    nextAvailableSlot : Address
    animats : map Address to (AST`Identifier * nat1)
    newBorns : map Address to (AST`Identifier * nat1)
    deads : set of Address
    valuesStorage : seq of (map Address to real)
    animatsStorage : seq of (map Address to (AST`Identifier * nat1))
    ticks : nat
init s ==
    s = mk_Memory({|->}, {|->}, {|->}, 1, {|->}, {|->}, {}, [], [], 0)
end

operations
    reset : () ==> ()
    reset() ==
        (vals := {|->};
        next := {|->};
        delta := {|->};
        nextAvailableSlot := 1;
        animats := {|->};
        newBorns := {|->};
        deads := {};
        valuesStorage := [];
        animatsStorage := [];
        ticks := 0);
    
    allocate : AST`Identifier * nat1 ==> Address
    allocate(identifier, size) ==
        let address : Address = nextAvailableSlot
        in
            (nextAvailableSlot := nextAvailableSlot + size;
            newBorns(address) := mk_(identifier, size);
            return address);
    
    deallocate : AST`Identifier * Address ==> ()
    deallocate(identifier, address) == deads := deads union {address};
    
    getAnimats : AST`Identifier ==> set of Address
    getAnimats(animatName) ==
        return {a | a in set dom animats
                & let mk_(n, -) = animats(a) in n = animatName};
    
    pure read : Address ==> real
    read(address) ==
        if address in set dom vals then return vals(address) else exit ADDRESS_ERROR;
    
    write : Address * real ==> ()
    write(address, data) == next(address) := data;
    
    writeDelta : Address * real ==> ()
    writeDelta(address, data) ==
        if
            address in set dom delta
        then
            delta(address) := delta(address) + data
        else
            exit ADDRESS_ERROR;
    
    pure getTime : () ==> nat
    getTime() == return ticks;
    
    setTime : nat1 ==> ()
    setTime(t) ==
        (if t = len valuesStorage + 1 then store();
        if t > len valuesStorage + 1 then exit TIME_ERROR;
        load(t);
        ticks := t);
    
    pure getLatestTime : () ==> nat
    getLatestTime() == return len valuesStorage;
    
    tick : () ==> ()
    tick() == setTime(getTime() + 1);
    
    store : () ==> ()
    store() ==
        (valuesStorage
            := valuesStorage
            ^ [{a |-> next(a) + (if a in set dom delta then delta(a) else 0)
            | a in set dom next \ deads}];
        animatsStorage
            := animatsStorage ^ [deads <-: animats munion newBorns]);
    
    load : nat1 ==> ()
    load(t) ==
        (vals := valuesStorage(t);
        animats := animatsStorage(t);
        next := vals;
        delta := {a |-> 0 | a in set dom vals};
        newBorns := {|->};
        deads := {});

end Memory
